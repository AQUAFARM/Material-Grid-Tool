/*
 * Copyright (C) 2016 Actinarium
 *
 * Licensed under the Apache License, Version 2.0 (the "License");
 * you may not use this file except in compliance with the License.
 * You may obtain a copy of the License at
 *
 *     http://www.apache.org/licenses/LICENSE-2.0
 *
 * Unless required by applicable law or agreed to in writing, software
 * distributed under the License is distributed on an "AS IS" BASIS,
 * WITHOUT WARRANTIES OR CONDITIONS OF ANY KIND, either express or implied.
 * See the License for the specific language governing permissions and
 * limitations under the License.
 */

apply plugin: 'com.android.library'
apply plugin: 'maven'

android {
    compileSdkVersion 23
    buildToolsVersion "23.0.2"

    defaultConfig {
        minSdkVersion 8
        targetSdkVersion 23
        versionCode project.versionCode
        versionName project.releaseVersion
    }
    buildTypes {
        release {
            minifyEnabled false
            proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.pro'
        }
    }
    lintOptions {
        disable 'RtlHardcoded','UnusedAttribute'
    }
}

configurations {
    javadocDeps
}

task androidJavadocs(type: Javadoc) {
    source = android.sourceSets.main.java.srcDirs
    classpath += configurations.javadocDeps
    classpath += project.files(android.bootClasspath)
}

task androidJavadocsJar(type: Jar, dependsOn: androidJavadocs) {
    classifier = 'javadoc'
    from androidJavadocs.destinationDir
}

task androidSourcesJar(type: Jar) {
    classifier = 'sources'
    from android.sourceSets.main.java.srcDirs
}

artifacts {
    archives androidSourcesJar
    archives androidJavadocsJar
}

afterEvaluate {
    androidJavadocs.classpath += files(android.libraryVariants.collect { variant ->
        variant.javaCompile.classpath.files
    })
}

uploadArchives {
    repositories.mavenDeployer {
        pom.groupId = 'com.actinarium.rhythm'
        pom.artifactId = 'rhythm-control'
        pom.version = project.releaseVersion
        pom.project {
            name 'Rhythm Control'
            description 'A module for Rhythm library that allows controlling Rhythm groups and overlays via a notification'
            url 'https://github.com/Actinarium/Rhythm'
            inceptionYear '2015'
            licenses {
                license {
                    name 'The Apache Software License, Version 2.0'
                    url 'http://www.apache.org/licenses/LICENSE-2.0.txt'
                    distribution 'repo'
                }
            }
            organization {
                name 'Actinarium'
                url 'http://actinarium.com'
            }
            developers {
                developer {
                    id 'Actine'
                    name 'Paul Danyliuk'
                    url 'https://plus.google.com/u/0/+PaulDanyliuk'
                    roles {
                        role 'architect'
                        role 'developer'
                    }
                }
            }
            scm {
                url 'https://github.com/Actinarium/Rhythm.git'
                connection 'scm:git:https://github.com/Actinarium/Rhythm.git'
                developerConnection 'scm:git:git@github.com:Actinarium/Rhythm.git'
            }
        }

        repository(url: "file://D:/Build")
    }
}

dependencies {
    compile fileTree(dir: 'libs', include: ['*.jar'])
    compile 'com.android.support:support-v4:23.3.0'
    compile project(':rhythm')
    javadocDeps 'com.android.support:support-v4:23.3.0'
    javadocDeps project(':rhythm')
}